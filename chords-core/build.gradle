import com.chrynan.chords.buildSrc.LibraryConstants

apply plugin: "kotlin-multiplatform"
apply plugin: "com.android.library"
apply plugin: "kotlinx-serialization"
apply plugin: "maven-publish"
apply plugin: "org.jetbrains.dokka"

group LibraryConstants.group
version LibraryConstants.versionName

kotlin {
    android()
    jvm()
    js(BOTH) {
        browser()
        nodejs()
    }
    ios()

    sourceSets {
        commonMain {
            dependencies {
                implementation "org.jetbrains.kotlin:kotlin-stdlib-common"

                implementation "org.jetbrains.kotlinx:kotlinx-serialization-json:1.2.1"

                api "com.chrynan.colors:colors-core:0.3.5"
            }
        }
        all {
            languageSettings.enableLanguageFeature("InlineClasses")
            languageSettings.useExperimentalAnnotation("kotlin.RequiresOptIn")
        }
    }
}

android {
    compileSdkVersion LibraryConstants.Android.compileSdkVersion

    defaultConfig {
        minSdkVersion LibraryConstants.Android.minSdkVersion
        targetSdkVersion LibraryConstants.Android.targetSdkVersion
        versionCode LibraryConstants.versionCode
        versionName LibraryConstants.versionName
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        debug {
            matchingFallbacks = ["release"]
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-rules.pro"
            matchingFallbacks = ["release"]
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }

    sourceSets {
        main {
            manifest.srcFile "src/androidMain/AndroidManifest.xml"
            java.srcDirs = ["src/androidMain/kotlin"]
            res.srcDirs = ["src/androidMain/res"]
        }
        test {
            java.srcDirs = ["src/androidTest/kotlin"]
            res.srcDirs = ["src/androidTest/res"]
        }
    }
}

dependencies {
    implementation "androidx.annotation:annotation:1.2.0"

    implementation "androidx.appcompat:appcompat:1.3.0"

    implementation "com.chrynan.parcelable:parcelable-core:0.3.1"
    implementation "com.chrynan.parcelable:parcelable-android:0.3.1"
}

configurations {
    compileClasspath
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

afterEvaluate {
    publishing {
        repositories {
            maven {
                url "https://repo.repsy.io/mvn/chrynan/public"

                credentials {
                    username = project.findProperty("repsyUsername") ?: System.getenv("repsyUsername")
                    password = project.findProperty("repsyToken") ?: System.getenv("repsyToken")
                }
            }
        }

        publications {
            release(MavenPublication) {
                from components.release
                groupId LibraryConstants.group
                artifactId "chords-core"
                version LibraryConstants.versionName
            }
        }
    }
}

// Documentation
tasks.named("dokkaGfm") {
    dokkaSourceSets {
        configureEach {
            includes.from("Module.md")
        }
    }
}
